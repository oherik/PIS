	#define	SIMULATOR
	#define	RUNFAST
	
	use	Labdefs.s12
	
IrqVector	equ	$3ff2
Stack_Drill	equ	$3000
Stack_Display	equ	$3100

	org	$1000
	
main:
; Ladda borrens stack
	lds	#Stack_Drill
; Avbrottsvektor, go!
	ldx	#IrqR
	stx	IrqVector
; Nolla gamla interrupts
	clr	ML19_AckIrq_1
	clr	ML19_AckIrq_2
; Gör så att avbrott tillåts (sätt i = 0)
	cli	
	
Main:	
	jsr	Process_Drill	; Börja med borrprocessen

Process_Drill:
	jsr	Drill_main	; Borrens huvudprogram
	inc	Var2
	movb	Var2,$c01
	bra	Process_Drill

Process_Display
	;jsr	DISPLAY
	inc	Var1		; Test
	movb	Var1,$c00	; Test
	bra	Process_Display

; Avbrottsrutin
;----------------------------------

IrqR:	
	ldab	ProcCounter	; Hämta vilket processnummer vi är på just nu
	clra			; Sätt D = B
	lslb			; Offset med 2 (double byte)
	ldy	#StackPointers	; Addressen till den första stackpekaren
	sts	d,y		; Spara nuvarande adress stackpekare (0 eller 2 plus adressen till första stackpekaren)
	
	inc	ProcCounter	; Öka process-talet för att visa på nästa process
	ldab	ProcCounter	; Läs in detta
	clra			; Sätt D = B
	ldx	#2		; Vi ska ta modulo(antal processer), dvs 2
	idiv			; Utför D modulo X, resten sparas i D
	stab	ProcCounter	; Spara resten till processnumret, detta kommer variera med 1 och 0 varannan gång
	
	lslb			; Offset med 2 (double byte)
	lds	d,y		; Sätt stackpekaren till nästa process stackpekare
	
	clr	ML19_AckIrq_1	; Nollställ interruptvippan
	rti			; Returnera
	
; Filer
;-----------------------------------

	use	Drill.s12
	use	Display_Ml5.s12

; Variabler
;-----------------------------------

; Räknar upp vilken process som körs. 0 = borr, 1 = display
ProcCounter	fcb	0
	
; Pekare till stackerna. Laddas initialt med den första stacken som initieras, men kan sedan ändras
StackPointers:	fdb	Stack_Drill,Stack_Display

; Temporära testvariabler	
Var1:	rmb	1	; För test
Var2:	rmb	1	; För test
	
; Stacks
;----------------------------------
	; Borr
	org	Stack_Drill
	fcb	$c0	;CC	(börjar med i=0)
	fcb	0	;B
	fcb	0	;A
	fdb	0	;X
	fdb	0	;Y
	fdb	Process_Drill	; Ska i början returneras till starten för drillprocessen

	;Display
	org	Stack_Display
	fcb	$c0	;CC	(börjar med i=0)
	fcb	0	;B
	fcb	0	;A
	fdb	0	;X
	fdb	0	;Y
	fdb	Process_Display	; Ska i början returneras till starten för displayprocessen
